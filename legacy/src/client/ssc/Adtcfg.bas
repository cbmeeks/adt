3D0G
NEW
REM THIS IS AN APPLESOFT PROGRAM, NOT A GW-BASIC PROGRAM.
10 REM ADT 1.23 CONFIGURATION
20 REM P. GUERTIN, 1994-10-13
30 REM UPDATED 2005-11-23
40 D$=CHR$(4)
50 IF PEEK(104)=64 THEN 90
60 POKE 103,1:POKE 104,64:POKE 16384,0
70 PRINT :PRINT "RELOADING AT $4000..."
80 PRINT D$"RUN ADT CONFIGURATION"
90 F$="ADT":REM    ADT FILENAME
100 L$="$A65":REM     ADT LENGTH
110 TEXT :HOME :SPEED=255
120 B(0)=300:B(1)=1200:B(2)=2400:B(3)=4800:B(4)=9600:B(5)=19200:B(6)=115200
130 B$(0)="Y":B$(1)="N"
140 INVERSE :PRINT " ADT 1.23 ";
150 NORMAL :PRINT "  DEFAULT CONFIGURATION"
160 VTAB 2:PRINT "----------------------------------------";
170 POKE 34,3:HOME
180 PRINT "LOADING FILE "F$"."
190 ONERR  GOTO 880
200 PRINT D$"BLOAD"F$",A$803"
210 ONERR  GOTO 890
220 IF PEEK(3786)<>49 OR PEEK(3788)<>50 OR PEEK(3789)<>51 THEN PRINT "BAD ADT, NOT VERSION 1.23, FAILURE":END
230 PRINT
240 FOR I=0 TO 7:V(I)=PEEK(2054+I):NEXT I
250 VTAB 6:PRINT "DISK SLOT (1-7): "V(0)+1
260 VTAB 6:INPUT "DISK SLOT (1-7): ";A$
270 IF A$="" THEN VTAB 6:PRINT "DISK SLOT (1-7): "V(0)+1:GOTO 300
280 A=VAL(A$):IF A<1 OR A>7 THEN PRINT "RANGE ERROR.":GOTO 250
290 V(0)=A-1
300 VTAB 7:PRINT "DISK DRIVE (1-2): "V(1)+1
310 VTAB 7:INPUT "DISK DRIVE (1-2): ";A$
320 IF A$="" THEN VTAB 7:PRINT "DISK DRIVE (1-2): "V(1)+1:GOTO 350
330 A=VAL(A$):IF A<1 OR A>2 THEN PRINT "RANGE ERROR.":GOTO 300
340 V(1)=A-1
350 VTAB 8:PRINT "SSC SLOT (1-7): "V(2)+1
360 VTAB 8:INPUT "SSC SLOT (1-7): ";A$
370 IF A$="" THEN VTAB 8:PRINT "SSC SLOT (1-7): "V(2)+1:GOTO 400
380 A=VAL(A$):IF A<1 OR A>7 OR A=V(0) THEN PRINT "RANGE ERROR.":GOTO 350
390 V(2)=A-1
400 PRINT :VTAB 9:PRINT "SSC SPEED (300-115200): "B(V(3))
410 VTAB 9:INPUT "SSC SPEED (300-115200): ";A$:IF A$="" THEN VTAB 9:PRINT "SSC SPEED (300-115200): "B(V(3)):GOTO 510
420 A=VAL(A$):V(3)=-1
430 IF A=300 THEN V(3)=0
440 IF A=1200 THEN V(3)=1
450 IF A=2400 THEN V(3)=2
460 IF A=4800 THEN V(3)=3
470 IF A=9600 THEN V(3)=4
480 IF A=19200 THEN V(3)=5
490 IF A=115200 THEN V(3)=6
500 IF V(3)=-1 THEN PRINT "RANGE ERROR.":PRINT "(300,1200,4800,9600,19200 OR 115200)":GOTO 410
510 VTAB 11:PRINT "                                    "
520 VTAB 10:PRINT "READ RETRIES (0-5, 10, 99): "V(4)
530 VTAB 10:INPUT "READ RETRIES (0-5, 10, 99): ";A$
540 IF A$="" THEN VTAB 10:PRINT "READ RETRIES (0-5, 10, 99): "V(4):GOTO 590
550 A=VAL(A$):IF A=10 THEN A=6
560 IF A=99 THEN A=7
570 IF A<0 OR A>7 THEN PRINT "RANGE ERROR.":GOTO 520
580 V(4)=A
590 VTAB 11:PRINT "WRITE RETRIES (0-5, 10, 99): "V(5)
600 VTAB 11:INPUT "WRITE RETRIES (0-5, 10, 99): ";A$
610 IF A$="" THEN VTAB 11:PRINT "WRITE RETRIES (0-5, 10, 99): "V(5):GOTO 660
620 A=VAL(A$):IF A=10 THEN A=6
630 IF A=99 THEN A=7
640 IF A<0 OR A>7 THEN PRINT "RANGE ERROR.":GOTO 600
650 V(5)=A
660 VTAB 12:PRINT "USE RWTS CHECKSUMS (Y/N): "B$(V(6))
670 VTAB 12:INPUT "USE RWTS CHECKSUMS (Y/N): ";A$
680 IF A$="" THEN VTAB 12:PRINT "USE RWTS CHECKSUMS (Y/N): "B$(V(6)):GOTO 720
690 IF A$="Y" THEN V(6)=0:GOTO 720
700 IF A$="N" THEN V(6)=1:GOTO 720
710 PRINT "RANGE ERROR.":GOTO 660
720 VTAB 13:PRINT "ENABLE SOUNDS (Y/N): "B$(V(7))
730 VTAB 13:INPUT "ENABLE SOUNDS (Y/N): ";A$
740 IF A$="" THEN VTAB 13:PRINT "ENABLE SOUNDS (Y/N): "B$(V(7)):GOTO 780
750 IF A$="Y" THEN V(7)=0:GOTO 780
760 IF A$="N" THEN V(7)=1:GOTO 780
770 PRINT "RANGE ERROR.":GOTO 720
780 PRINT :INPUT "S)AVE, R)ESTART, Q)UIT? ";A$
790 IF A$="S" THEN 830
800 IF A$="R" THEN 110
810 IF A$<>"Q" THEN 780
820 GOTO 870
830 PRINT :PRINT "SAVING FILE "F$"."
840 FOR I=0 TO 7:POKE 2054+I,V(I):NEXT
850 PRINT D$"BSAVE"F$",A$803,L"L$
860 PRINT :PRINT "FILE "F$" SAVED."
870 POKE 216,0:TEXT :END
880 PRINT "ERROR: FILE "F$" NOT FOUND!":END
890 PRINT "UNEXPECTED ERROR. SORRY.":END
